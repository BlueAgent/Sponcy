buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }

}

plugins {
    id "com.github.johnrengelman.shadow" version "1.2.4"
}

apply plugin: 'net.minecraftforge.gradle.forge'

repositories {
    maven {
        // location of the maven that hosts JEI files
        url = "http://dvs1.progwml6.com/files/maven"
    }
}

version = "${mod_version}"
group = "${mod_id}"
archivesBaseName = "${mod_id}"

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

minecraft {
    version = "${mc_version}-${forge_version}"
    runDir = "run"

    mappings = "${mappings_version}"
    makeObfSourceJar = false

    replaceIn "src/main/java/spontaneouscollection/SpontaneousCollection.java"
    replace "MOD_VERSION = \"99999.999.999\"", "MOD_VERSION = \"${mod_version}\""
    replace "MC_VERSION = \"\"", "MC_VERSION = \"[${mc_version}]\""
    replace "DEPENDENCIES = \"\"", "DEPENDENCIES = \"required-after:Forge@[${forge_version},)\""
}

dependencies {
    //compile "some.group:artifact:version:classifier"
    //compile "some.group:artifact:version"
    //compile 'com.mod-buildcraft:buildcraft:6.0.8:dev'  // adds buildcraft to the dev env
    //compile 'com.googlecode.efficient-java-matrix-library:ejml:0.24' // adds ejml to the dev env
    //deobfCompile 'com.mod-buildcraft:buildcraft:6.0.8:dev'
    //deobfProvided 'com.mod-buildcraft:buildcraft:6.0.8:dev'

    testCompile "junit:junit:${junit_version}"

    // compile against the JEI API
    deobfCompile "mezz.jei:jei_${mc_version}:${jei_version}:api"
    // at runtime, use the full JEI jar
    runtime "mezz.jei:jei_${mc_version}:${jei_version}"

    //SQLiteJDBC
    compile "org.xerial:sqlite-jdbc:${sqlite_jdbc_version}"
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

shadowJar {
    classifier = ""
    exclude "dummyThing"

    relocate "org.sqlite", "spontaneouscollection.shaded.org.sqlite"

    dependencies {
        include(dependency("org.xerial:sqlite-jdbc:${sqlite_jdbc_version}"))
    }
}

reobf {
    shadowJar {
        mappingType = "SEARGE"
    }
}

tasks.reobfShadowJar.mustRunAfter shadowJar
